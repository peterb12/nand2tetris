// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in = regOut, out = incOut);
    // Truth table for those input bits....
    // Reset | Load | Inc   |    out
    //  0      0       0    |     keep PC the same
    //  0      0       1    |     increment PC
    //  0      1       0    |     load incoming value
    //  0      1       1    |     load incoming value
    //  1      0       0    |     0
    //  1      0       1    |     0
    //  1      1       0    |     0
    //  1      1       1    |     0
    Mux8Way16(
        a = regOut,
        b = incOut,
        c = in,
        d = in,
        e = false,
        f = false,
        g = false,
        h = false,
        sel[0] = inc, sel[1] = load, sel[2] = reset,
        out = muxOut
        );
    Register(in = muxOut, load = true, out = regOut, out = out);
}
